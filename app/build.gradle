apply plugin: "com.android.application"
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.room'

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    defaultConfig {
        compileSdk 36
        applicationId "com.mikifus.padland"
        minSdkVersion 21
        targetSdkVersion 36
        versionName '3.3'
        versionCode 30
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    // used by Room, to test migrations
    sourceSets {
        getByName("androidTest").assets.srcDir("$projectDir/schemas")
    }

    buildTypes {
        debug {
            pseudoLocalesEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
        disable 'InvalidPackage'
    }
    namespace 'com.mikifus.padland'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

room {
    schemaDirectory("$projectDir/schemas")
}

dependencies {
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'com.github.paolorotolo:appintro:6.3.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.recyclerview:recyclerview-selection:1.2.0'
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation 'com.github.martin-stone:hsv-alpha-color-picker-android:3.1.0'
    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'androidx.webkit:webkit:1.14.0'

    // Room database
    def room_version = "2.7.2"

    implementation("androidx.room:room-runtime:$room_version")
    annotationProcessor("androidx.room:room-compiler:$room_version")

    // To use Kotlin annotation processing tool (kapt)
    kapt("androidx.room:room-compiler:$room_version")
    // To use Kotlin Symbol Processing (KSP)
    annotationProcessor("androidx.room:room-compiler:$room_version")

//    // optional - Kotlin Extensions and Coroutines support for Room
    implementation("androidx.room:room-ktx:$room_version")

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.9.4"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.9.4"


    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }

    // 'androidTest' dependencies for instrumentation (tests on device).
    // These are for room database migration test.
    androidTestImplementation "androidx.test.ext:junit-ktx:1.3.0"
    androidTestImplementation "androidx.room:room-testing:2.8.1"
    androidTestImplementation "androidx.test:runner:1.7.0"


    // 'test' dependencies to run unit tests from the IDE
    testImplementation "androidx.test.ext:junit-ktx:1.3.0"
    testImplementation 'org.robolectric:robolectric:4.15.1'

    // AndroidJUnitRunner and JUnit Rules
    testImplementation "androidx.test:runner:1.7.0"
    testImplementation "androidx.test:rules:1.7.0"

}
